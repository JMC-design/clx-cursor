(in-package :clx-cursor)

(defun render-create-anim-cursor (cursors delays)
  "Create animated cursor. cursors length must be the same as delays length."
  (let ((display (xlib:cursor-display (first cursors))))
    (xlib::ensure-render-initialized display)
    (let* ((cursor (xlib::make-cursor :display display))
           (cid (xlib::allocate-resource-id display cursor 'cursor))
           (cursors-length (length cursors))
           (cursors-delays (make-list (* 2 (length cursors)))))
      (setf (xlib:cursor-id cursor) cid)
      (dotimes (i cursors-length)
        (setf (elt cursors-delays (* 2 i)) (xlib:cursor-id (elt cursors i))
              (elt cursors-delays (1+ (* 2 i))) (elt delays i)))
      (xlib::with-buffer-request (display (xlib::extension-opcode display "RENDER"))
        (data xlib::+X-RenderCreateAnimCursor+)
        (resource-id cid)
        ((sequence :format xlib:card32) cursors-delays))
      cursor)))
